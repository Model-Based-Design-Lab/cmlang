
grammar info.computationalmodeling.lang.MaxPlusMatrix with org.eclipse.xtext.common.Terminals
generate maxplusmatrix "http://www.computationalmodeling.info/lang/MaxPlusMatrix"


MaxPlusMatrixModel:
	'max-plus' 'model' name=ID ':'
	('matrices' (matrices += MaxPlusMatrix)+)?
	('vector sequences' (vectorsequences += VectorSequence)+)?
	('event sequences' (eventsequences += EventSequence)+)?
	;

MaxPlusMatrix:
	name=ID 
	(labels = Labels)?
	'='
	'['
	(rows += Row (',')?)*
	']'
;

Row:
	'[' ( elements += Element (',')?)* ']'
;

VectorSequence:
	name=ID 
	(labels = Labels)?
	'='
	'['
	(vectors += Row (',')?)*
	']'
;

EventSequence:
	name=ID '='
	sequence = Row
;

Labels:
	'('
	(label += ID (',')?)+	
	')'
;

Element:
	num = Number | inf = '-inf'
;

Number:
	ratio=Ratio | value=(PNInt | Float)
;

Ratio:
	numerator=PNInt '/' denominator=INT
;

PNInt:
	INT | NINT
;

terminal NINT:
	'-' INT
;

terminal Float: 
  '-'? INT? '.' INT (('E'|'e') '-'? INT)?
;
