grammar info.computationalmodeling.lang.MarkovChains with org.eclipse.xtext.common.Terminals
generate markovchains "http://www.computationalmodeling.info/lang/MarkovChains"

MarkovChainModel:
	('author' '=' author=ID)?
	'markov' 'chain' name=ID '{'
	edges += Edge*
	'}'
;

Edge:
        (srcstate=State ('-')* '->' dststate=State) | (srcstate=State ('-')+ (specs=EdgeSpecs) ('-')* '->' dststate=State)
;

EdgeSpecs:
	{EdgeSpecs} 
	annotations += EdgeAnnotation (';' annotations += EdgeAnnotation)*
;

EdgeAnnotation:
	('probability' ':' probability=Probability)
	|
	(probability=Probability) 
;

State:
	name=ID
	(specs=StateSpecs)?
;

StateSpecs:
	{StateSpecs} '[' 
	annotations += StateAnnotation (';' annotations += StateAnnotation)*
	']'
;

StateAnnotation:
	(((('initial' 'probability') | ('p')) ':') initprob=Probability) |
	(((('reward') | ('r')) ':') reward=Reward) 	 
;

Probability:
	ratio=PosRatio | float=Float | int=INT  
;

PosRatio:
	numerator=INT '/' denominator=INT
;

Reward:
	ratio=PosNegRatio | float=Float | int=INT| negint = NEGINT
;


PosNegRatio:
	(numerator=INT | negnumerator=NEGINT) '/' denominator=INT
;

terminal NEGINT: 
  '-' INT
; 

terminal Float: 
  '-'? INT? '.' INT (('E'|'e') '-'? INT)?
;  